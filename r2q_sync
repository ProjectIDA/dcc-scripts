#! /bin/tcsh
#
# This script is to send sync files generated by r2q_send to DMC."
#
if ($#argv != 1) then
HELP:
  echo ""
  echo "  ========================="
  echo "  usage: r2q_sync go"
  echo "  ========================="
  echo ""
  echo "  r2q_sync is to send sync files generated by r2q_send to DMC."
  echo ""
  echo "  sync files generated by r2q_send are stored in ${GSN_STORAGE_ROOT}/tmp/daily/sync with '.sync' file extension."
  echo "  sync files found in ${GSN_STORAGE_ROOT}/tmp/daily/sync will be sent to DMC in a tar file every Wednesday via"
  echo "  a cron job running by user idadcc@idadcc.  After the tar file is sent, sync files and the tar"
  echo "  file are moved to a subdirectory DONE.syncfile/, i.e. ${GSN_STORAGE_ROOT}/tmp/daily/sync/DONE.syncfile."
  echo ""
  exit
else
  if ($argv[1] != 'go') then
    goto HELP
  endif
endif

set ida_admin = 'erik.klimczak@earthscope.org,dan.auerbach@earthscope.org'
#
# go to where the sync files are
#
cd ${GSN_STORAGE_ROOT}/tmp/daily/sync

ls *.sync >& /dev/null
if ($status != 0) then
  echo ""
  echo r2q_sync: no sync files found in `pwd`.
  ###MIGRATION  Mail -s "no sync files found in `pwd`" $ida_admin < /dev/null >& /dev/null
  echo ""
  exit
endif

#
# find the day boundaries of these sync files:
#
set d1 = `ls *.sync | cut -c1-10| sort -u | awk -F'-' '{print $1$2$3}' |head -1`
set d2 = `ls *.sync | cut -c1-10| sort -u | awk -F'-' '{print $1$2$3}' |tail -1`
set tarfile = II.sync.$d1-$d2.tar

#
# create a tar file for all available sync files
#
tar -cf $tarfile *.sync
if ($status != 0) then
  echo ""
  echo r2q_sync: Error creating tar file: $tarfile.
  ###MIGRATION Mail -s "Error tar'ing sync files found in `pwd`" $ida_admin < /dev/null >& /dev/null
  echo ""
  exit
endif

#
# mail the tar file to DMC
# Per email from Anh on Sept 6, 2023, email notification no longer required
#

#
# check for DONE.syncfile directory
#
if (! -d DONE.syncfile) mkdir DONE.syncfile
if ($status != 0) then
  echo ""
  echo r2q_sync: Error creating DONE.syncfile directory in `pwd`.
  ###MIGRATION Mail -s "Error creating DONE.syncfile directory in `pwd`" $ida_admin < /dev/null >& /dev/null
  echo ""
  exit
endif

#
# copy tar file to DMC web pickup dir
#
echo
if (! $?IDA_WEB_ROOT) then
  echo "IDA_WEB_ROOT env variable is undefined. Can not copy tarfile ${tarfile} to DMC pickup dir."
else
  if ("$GSN_STORAGE_ROOT" == "/ida") then
     set dmc_pickup_dir = ${IDA_WEB_ROOT}/pickup/sync
     cp $tarfile ${dmc_pickup_dir}/
  else
     set dmc_pickup_dir = s3://gsn-status-prod-us-east-2-jhvessr3v5ku/sync/dmc/
     aws s3 cp $tarfile ${dmc_pickup_dir}
  endif
  if ($status != 0) then
    echo ""
    echo r2q_sync: Error copying tar file $tarfile to ${dmc_pickup_dir}.
    ###MIGRATION  Mail -s "Error copying tar file $tarfile to ${dmc_pickup_dir}" $ida_admin < /dev/null >& /dev/null
    echo ""
  endif
endif

#
# move tar file to DONE.syncfile
#
mv $tarfile DONE.syncfile
if ($status != 0) then
  echo ""
  echo r2q_sync: Error moving tar file: $tarfile.
  ###MIGRATION  Mail -s "Error moving $tarfile to DONE.syncfile" $ida_admin < /dev/null >& /dev/null
  echo ""
  exit
endif

rm *.sync
