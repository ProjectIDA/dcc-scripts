#! /bin/tcsh
#
# This script is to send SYNC files generated by r2q_send to DMC."
#
if ($#argv != 1) then
HELP:
  echo ""
  echo "  ========================="
  echo "  usage: r2q_sync go"
  echo "  ========================="
  echo ""
  echo "  r2q_sync is to send SYNC files generated by r2q_send to DMC."
  echo ""
  echo "  SYNC files generated by r2q_send are stored in /ida/tmp/daily/SYNC with '.sync' file extension."
  echo "  SYNC files found in /ida/tmp/daily/SYNC will be sent to DMC in a tar file every Wednesday via"
  echo "  a cron job running by user idadcc@idadcc.  After the tar file is sent, SYNC files and the tar"
  echo "  file are moved to a subdirectory DONE.syncfile/, i.e. /ida/tmp/daily/SYNC/DONE.syncfile."
  echo ""
  exit
else
  if ($argv[1] != 'go') then
    goto HELP
  endif
endif

#
# go to where the SYNC files are
#
cd /ida/tmp/daily/SYNC

ls *.sync >& /dev/null
if ($status != 0) then
  echo ""
  echo r2q_sync: no SYNC files found in `pwd`.
  set admin = 'ezak@ucsd.edu, dauerbach@ucsd.edu'
  Mail -s "no SYNC files found in `pwd`" $admin < /dev/null >& /dev/null
  echo ""
  exit
endif

#
# find the day boundaries of these SYNC files:
#
set d1 = `ls *.sync | cut -c1-10| sort -u | awk -F'-' '{print $1$2$3}' |head -1`
set d2 = `ls *.sync | cut -c1-10| sort -u | awk -F'-' '{print $1$2$3}' |tail -1`
set tarfile = II.SYNC.$d1-$d2.tar

#
# create a tar file for all available SYNC files
#
tar -cf $tarfile *.sync

#
# mail the tar file to DMC
#
set admin = 'maryann@iris.washington.edu, dauerbach@ucsd.edu, ezak@ucsd.edu'
Mail -s "SYNC files from $d1 to $d2" $admin < $tarfile 

#
# move files to DONE.syncfile
#
if (! -d DONE.syncfile) mkdir DONE.syncfile
mv *.sync $tarfile DONE.syncfile

