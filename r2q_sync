#! /bin/tcsh
#
# This script is to send SYNC files generated by r2q_send to DMC."
#
if ($#argv != 1) then
HELP:
  echo ""
  echo "  ========================="
  echo "  usage: r2q_sync go"
  echo "  ========================="
  echo ""
  echo "  r2q_sync is to send SYNC files generated by r2q_send to DMC."
  echo ""
  echo "  SYNC files generated by r2q_send are stored in /ida/tmp/daily/SYNC with '.sync' file extension."
  echo "  SYNC files found in /ida/tmp/daily/SYNC will be sent to DMC in a tar file every Wednesday via"
  echo "  a cron job running by user idadcc@idadcc.  After the tar file is sent, SYNC files and the tar"
  echo "  file are moved to a subdirectory DONE.syncfile/, i.e. /ida/tmp/daily/SYNC/DONE.syncfile."
  echo ""
  exit
else
  if ($argv[1] != 'go') then
    goto HELP
  endif
endif

#
# for ASL to pick up week's worth of IDA/II 
# sync file to know what to pull from DMC
#
set aslsyncdir = /sync

#
# go to where the SYNC files are
#
cd /ida/tmp/daily/SYNC

ls *.sync >& /dev/null
if ($status != 0) then
  echo ""
  echo r2q_sync: no SYNC files found in `pwd`.
  set admin = 'ezak@ucsd.edu,sbargabus@ucsd.edu,dauerbach@ucsd.edu'
  Mail -s "no SYNC files found in `pwd`" $admin < /dev/null >& /dev/null
  echo ""
  exit
endif

#
# cp sync files to shared ASL shared space
# 
cp *.sync $aslsyncdir >& /dev/null
if ($status != 0) then
  echo ""
  echo r2q_sync: Error copying SYNC files to $aslsyncdir.
  set admin = 'ezak@ucsd.edu,sbargabus@ucsd.edu,dauerbach@ucsd.edu'
  Mail -s "Error copying SYNC files to $aslsyncdir" $admin < /dev/null >& /dev/null
  echo ""
  exit
endif

#
# find the day boundaries of these SYNC files:
#
set d1 = `ls *.sync | cut -c1-10| sort -u | awk -F'-' '{print $1$2$3}' |head -1`
set d2 = `ls *.sync | cut -c1-10| sort -u | awk -F'-' '{print $1$2$3}' |tail -1`
set tarfile = II.SYNC.$d1-$d2.tar

#
# create a tar file for all available SYNC files
#
tar -cf $tarfile *.sync
if ($status != 0) then
  echo ""
  echo r2q_sync: Error creating tar file: $tarfile.
  set admin = 'ezak@ucsd.edu,sbargabus@ucsd.edu,dauerbach@ucsd.edu'
  Mail -s "Error tar'ing SYNC files found in `pwd`" $admin < /dev/null >& /dev/null
  echo ""
  exit
endif

#
# mail the tar file to DMC
#
set admin = 'maryann@iris.washington.edu, dauerbach@ucsd.edu, ezak@ucsd.edu, sbargabus@ucsd.edu'
Mail -s "SYNC files from $d1 to $d2" $admin < $tarfile 

#
# check for DONE.syncfile directory
#
if (! -d DONE.syncfile) mkdir DONE.syncfile
if ($status != 0) then
  echo ""
  echo r2q_sync: Error creating DONE.syncfile directory in `pwd`.
  set admin = 'ezak@ucsd.edu,sbargabus@ucsd.edu,dauerbach@ucsd.edu'
  Mail -s "Error creating DONE.syncfile directory in `pwd`" $admin < /dev/null >& /dev/null
  echo ""
  exit
endif

#
# move tar file to DONE.syncfile
#
mv $tarfile DONE.syncfile



